require conf/distro/poky-altcfg.conf

include conf/distro/include/amd-edf-feed.conf

include conf/distro/include/amd-edf-mirrors.conf

# Because we are no longer strictly poky-altcfg, set distro variables

DISTRO = "amd-edf"
DISTRO_NAME = "AMD Embedded Development Framework Linux distribution"

# Define the default hostname
hostname:pn-base-files:amd-edf = "amd-edf"

# Based on poky-altcfg
DISTROOVERRIDES = "poky:poky-altcfg:amd-edf"

AMD-EDF_VERSION = "25.11"
AMD-EDF_MINOR = ""

# Set to "development" or "release" for release
AMD-EDF_VER_TYPE = "release"

# This will be set in local.conf for CI builds
AMD-EDF_VER_BUILD ?= "${@'${METADATA_REVISION}' if d.getVar('METADATA_REVISION') != '<unknown>' else 'unknown'}"

# Follow the Poky format of <Version>+<type>-<date>
DISTRO_VERSION = "${AMD-EDF_VERSION}${AMD-EDF_MINOR}+${AMD-EDF_VER_TYPE}-${AMD-EDF_VER_BUILD}"

SDK_VENDOR = "-amdedfsdk"

MAINTAINER = "AMD AECG <meta-xilinx@yoctoproject.org>"

TARGET_VENDOR = "-amd"

# Define the ROS2 Yocto release
ROS_OE_RELEASE_SERIES = "scarthgap"

# Define ROS2 distro release
ROS_DISTRO = "jazzy"

# While microblaze (classic) is not supported, we want to ensure it can
# be built if needed
POKY_INIT_MANAGER:amd-edf:microblaze = "sysvinit"
AMD-EDF_DISTRO_FEATURES:microblaze = ""

AMD-EDF_DISTRO_FEATURES = " \
    multiarch \
    security \
    virtualization \
    "

AMD-EDF_DISTRO_FEATURES:append:arm = " \
    vmsep \
    "

AMD-EDF_DISTRO_FEATURES:append:aarch64 = " \
    openamp \
    tpm \
    vmsep \
    xen \
    efi \
    polkit \
    rauc \
    "

DISTRO_FEATURES:append = " ${AMD-EDF_DISTRO_FEATURES}"

# Ensure rauc is enabled fully, this will require a temporary override and setting
SYSTEM_CONTROLLER_RAUC_DEMO = "yes"
DISTROOVERRIDES .= "${@ ':system-controller-rauc-demo' if bb.utils.to_boolean(d.getVar('SYSTEM_CONTROLLER_RAUC_DEMO'), False) else ''}"

# Set EFI_PROVIDER to systemd-boot for EDF by default
EFI_PROVIDER ?= "systemd-boot"

PREFERRED_RPROVIDER_virtual-systemd-bootconf = "systemd-bootconf-edf"

# The following really should not refer to MACHINE_FEATURES, but there isn't
# a better way at this point
DISTRO_FEATURES:append = "${@bb.utils.contains('MACHINE_FEATURES', 'mali400', ' libmali', '', d)}"
DISTRO_FEATURES:append = "${@bb.utils.contains('MACHINE_FEATURES', 'malig78ae', ' libmali', '', d)}"
DISTRO_FEATURES[vardepsexclude] += "MACHINE_FEATURES"

# Disable Busybox - Based on Poky example here:
# poky/meta-poky/conf/templates/default/local.conf.sample.extended

PREFERRED_PROVIDER_base-utils = "packagegroup-core-base-utils"
VIRTUAL-RUNTIME_base-utils = "packagegroup-core-base-utils"
VIRTUAL-RUNTIME_base-utils-hwclock = "util-linux-hwclock"
VIRTUAL-RUNTIME_base-utils-syslog = ""

# Enable license package generation
LICENSE_CREATE_PACKAGE = "1"

# Set start-jupyterlab as default start daemon.
PREFERRED_PROVIDER_start-jupyter = "start-jupyterlab"

# Boot script only required for Zynq, ZynqMP and Kria devices.
UBOOT_BOOT_SCRIPT_SOC_DEFAULT = ""
UBOOT_BOOT_SCRIPT_SOC_DEFAULT:zynq = "u-boot-edf-scr"
UBOOT_BOOT_SCRIPT_SOC_DEFAULT:zynqmp = "u-boot-edf-scr"
UBOOT_BOOT_SCRIPT_SOC_DEFAULT:kria:zynqmp = "u-boot-xlnx-scr"

# Allow this to be easily overridden by an image
UBOOT_BOOT_SCRIPT = "${UBOOT_BOOT_SCRIPT_SOC_DEFAULT}"

# Configure default users/groups
# Default rules (assumed no debug-tweaks image feature):
# * disabled root login (set by system default)
# * Add a user 'amd-edf' with no password
#   - SSH won't allow passwd-less login w/o debug tweaks, so requires console login
#   - Set to immediately expire
#   - Add to the sudoers file
# * Add 'amd-edf' user to the audio group
# * Add 'amd-edf' user to the video group
IMAGE_CLASSES += "extrausers"
EXTRA_USERS_PARAMS ?= "\
    useradd -p '' amd-edf;passwd-expire amd-edf; \
    groupadd -r aie; \
    groupadd -r wayland; \
    usermod -a -G aie,audio,input,video,wayland amd-edf; \
"
USERADDEXTENSION:append = " amd-useradd-sudoers"
EXTRA_USERS_SUDOERS ?= "amd-edf ALL=(ALL) ALL;"

# Disable OpenAMP device tree overlay package
ENABLE_OPENAMP_DTSI = "${@bb.utils.contains('DISTRO_FEATURES', 'openamp', '2', '0', d)}"

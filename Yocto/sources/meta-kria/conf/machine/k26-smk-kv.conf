#@TYPE: Machine
#@NAME: Generic Kria KV260 Vision AI Starter Kit
#@DESCRIPTION: Machine configuration for Kria KV260 Vision AI Starter Kit (Kria K26 SOM + KV260 carrier board).

#### Preamble
MACHINEOVERRIDES =. "${@['', 'k26-smk-kv:']['k26-smk-kv' != '${MACHINE}']}"
MACHINEOVERRIDES =. "k26-kv:"
#### Regular settings follow

require conf/machine/include/k26-smk.inc
require conf/machine/include/kria-qemu-kv.inc

INITRAMFS_IMAGE ?= "petalinux-initramfs-image"

# This is an 'XSCT' based BSP
XILINX_XSCT_VERSION = "2025.2"
XILINX_WITH_ESW = "xsct"

# Add system XSA
HDF_URI = 'https://edf.amd.com/sswreleases/rel-v2025.2/hdf-examples/2025.2/2025.2_1115_1_11150546/external/k26-smk-kv/k26-smk-kv-2025.2_1115_1.xsa'
HDF_URI[sha256sum] = 'efa21f466639e2bf5045734e472709d247a6cea5218b875dcc2d2aa4dfe37a3c'

UBOOT_DT_FILES = "\
    zynqmp-sck-kv-g-revA.dtso \
    zynqmp-sck-kv-g-revB.dtso \
    "

# DTB_FILE_NAME cant be changed globally or it will break the build but we can modify it for these
# tasks specifically so that the resulting symlink points to the correct file for a specific board
DTB_FILE_NAME:task-install:pn-device-tree:linux = "${UBOOT_DTFILE_PREFIX}-zynqmp-sck-kv-g-revB.dtb"
DTB_FILE_NAME:task-deploy:pn-device-tree:linux = "${UBOOT_DTFILE_PREFIX}-zynqmp-sck-kv-g-revB.dtb"
# Workaround for bitbake not detecting the above change in the hash
PR:append:pn-device-tree = ".1"

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' k26_smk_kv']['k26-smk-kv' != "${MACHINE}"]}"
